import com.google.gson.JsonParser
import com.google.gson.JsonPrimitive

plugins {
    id "fabric-loom"
    id "maven-publish"
    id 'com.palantir.git-version'
}

var targetJdk = Integer.parseInt(project.java_compiler_version)

allprojects {
    version = "${project.mod_version}+${versionDetails().gitHash[0..7]}~lonefelidae16"
    group = project.maven_group

    repositories {
        maven {
            url = uri("https://api.modrinth.com/maven/")
        }
        maven {
            name = "stashymane's repo"
            url = "https://repo.stashy.dev/releases"
        }
        maven {
            url = "https://artifactory.kow08absty.com/artifactory/fabricmc"
        }
    }
}

base {
    archivesName = project.archives_base_name
}

allprojects {
    apply plugin: 'java'

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.toVersion(targetJdk)
        targetCompatibility = JavaVersion.toVersion(targetJdk)
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release.set(targetJdk)
    }

    project.ext['moduleId'] = 'unnamed'
    project.ext['moduleName'] = ''
    project.ext['mod_license'] = project.mod_license
    project.ext['moduleAccessWidener'] = ''

    tasks.withType(ProcessResources).configureEach {
        doFirst {
            var props = [
                    "mod_page",
                    "mod_sources",
                    "mod_issues",
                    "mod_license",
                    "mod_support_lowest_mc"
            ].collectEntries {
                [it, project.properties[it]]
            }

            props.putAll([
                    'moduleId'   : project.ext['moduleId'],
                    'moduleName' : project.ext['moduleName'],
                    'mod_license': project.ext['mod_license'],
                    'version'    : project.version
            ])

            filesMatching("fabric.mod.json") {
                expand props
            }
        }

        doLast {
            fileTree(dir: outputs.files.asPath, include: "**/fabric.mod.json").each { file ->
                var fabricModJson = JsonParser.parseString(file.text).asJsonObject
                if (!project.ext['moduleAccessWidener'].isEmpty()) {
                    fabricModJson.add("accessWidener", new JsonPrimitive(project.ext['moduleAccessWidener']))
                    file.text = fabricModJson.toString()
                }
            }
        }
    }
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 10, 'minutes'
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.minecraft_version}${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    modImplementation include("me.lonefelidae16:groominglib:${project.groom_version}") {
        changing = true
    }

    // Extra Sounds modules
    implementation include(project(path: 'logics', configuration: 'namedElements'))
    // 1.21 family
    implementation include(project(path: 'versioned:es1.21', configuration: 'namedElements'))
    // 1.20 family
    implementation include(project(path: 'versioned:es1.20.5', configuration: 'namedElements'))
    implementation include(project(path: 'versioned:es1.20.2', configuration: 'namedElements'))
    implementation include(project(path: 'versioned:es1.20', configuration: 'namedElements'))
    // 1.19 family
    implementation include(project(path: 'versioned:es1.19.4', configuration: 'namedElements'))
    implementation include(project(path: 'versioned:es1.19.3', configuration: 'namedElements'))
    implementation include(project(path: 'versioned:es1.19.1', configuration: 'namedElements'))
    implementation include(project(path: 'versioned:es1.19', configuration: 'namedElements'))
    // 1.18 family
    implementation include(project(path: 'versioned:es1.18.2', configuration: 'namedElements'))
    implementation include(project(path: 'versioned:es1.18', configuration: 'namedElements'))

    // Sound Categories modules
    implementation include(project(path: 'sound-categories', configuration: 'namedElements'))
    implementation project(path: 'sound-categories:shared', configuration: 'namedElements')
    // 1.21 family
    implementation project(path: 'sound-categories:versioned:sc1.21', configuration: 'namedElements')
    // 1.20 family
    implementation project(path: 'sound-categories:versioned:sc1.20.5', configuration: 'namedElements')
    implementation project(path: 'sound-categories:versioned:sc1.20.3', configuration: 'namedElements')
    implementation project(path: 'sound-categories:versioned:sc1.20.2', configuration: 'namedElements')
    implementation project(path: 'sound-categories:versioned:sc1.20', configuration: 'namedElements')
    // 1.19 family
    implementation project(path: 'sound-categories:versioned:sc1.19.4', configuration: 'namedElements')
    implementation project(path: 'sound-categories:versioned:sc1.19.3', configuration: 'namedElements')
    implementation project(path: 'sound-categories:versioned:sc1.19', configuration: 'namedElements')
    // 1.18 family
    implementation project(path: 'sound-categories:versioned:sc1.18', configuration: 'namedElements')
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archives_base_name}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        maven {
            url = "https://repo.stashy.dev"
            credentials {
                username = project.hasProperty("publish_username") ? project.publish_username : ""
                password = project.hasProperty("publish_password") ? project.publish_password : ""
            }
        }
    }
}
